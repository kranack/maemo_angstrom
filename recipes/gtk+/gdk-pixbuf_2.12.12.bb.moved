DESCRIPTION = "Stand-alone libpixbuf, a pixelmap image i/o library from the \
GTK+ multi-platform toolkit for creating graphical user interfaces."
HOMEPAGE = "http://www.gtk.org/"
SECTION = "libs"
LICENSE = "LGPL"
PRIORITY = "optional"
DEPENDS = "libpng3 gettext glib-2.0"
PR = "r101"

S = "${WORKDIR}/gtk+2.0-${PV}/"
FILESPATH = "${FILE_DIRNAME}/gdk-pixbuf-csource:${FILE_DIRNAME}/gtk+-${PV}:${FILE_DIRNAME}/files"

SRC_URI = " http://repository.maemo.org/pool/fremantle/free/g/gtk+2.0/gtk+2.0_${PV}-1maemo12+0m5.tar.gz \
            file://gtk+2.0-2.12.12/png-use-old-symbol.patch;patch=1;pnum=0 \
            file://automake-lossage.patch;patch=1 "

  
inherit autotools pkgconfig

LIBV = "2.10.0"

FILES_${PN} = "${bindir}/gdk-pixbuf-query-loaders \
	${bindir}/gtk-update-icon-cache \
	${libdir}/lib*.so.*"

FILES_${PN}-dev += " \
    ${datadir}/gtk-2.0/include \
	${libdir}/gtk-2.0/include \
	${libdir}/gtk-2.0/${LIBV}/loaders/*.la \
	${bindir}/gdk-pixbuf-csource \
	${bindir}/gtk-builder-convert"

FILES_${PN}-dbg += " \
        ${libdir}/gtk-2.0/${LIBV}/loaders/.debug/*"

EXTRA_OECONF = "\
  --with-gdktarget=x11 \
  --without-libtiff \
  --without-libjpeg \
  --with-libpng \
"

EXTRA_OECONF += "--disable-modules --with-included-loaders=png,tga --disable-gtk-doc"

# check for TARGET_FPU=soft and inform configure of the result so it can disable some floating points
require gtk-fpu.inc
EXTRA_OECONF += "${@get_gtk_fpu_setting(bb, d)}"


do_configure_prepend(){
    touch gtk-doc.make
}


do_compile() {
	cd gdk-pixbuf && oe_runmake
}

do_stage() {
	oe_libinstall -C gdk-pixbuf -so libgdk_pixbuf-2.0 ${STAGING_LIBDIR}
	cd gdk-pixbuf && oe_runmake install DESTDIR=${STAGING_DIR_HOST}${layout_exec_prefix}
	autotools_stage_includes
#	install -d -m 0755 ${STAGING_LIBDIR}/gtk-2.0/include
#	install -m 0644 gdk/gdkconfig.h ${STAGING_LIBDIR}/gtk-2.0/include/gdkconfig.h
}

do_install() {
	oe_libinstall -C gdk-pixbuf -so libgdk_pixbuf-2.0 ${D}/${libdir}
	cd gdk-pixbuf && oe_runmake install DESTDIR=${D}
}

